//Admin tools by Infinity (made from the beginning since lack of compability between versions)

//Take admin on the servers you have admin with the same key
//If you are not on your nick, when you press P to get admin, or when you perform an admin tools command, you will show your admin name, and then turn back to the previous nick (to make it easier for hosts or reporting to know who was it)
//If you perform admin tools commands with a weak reason, it will transform to a valid generic one (to make it faster to ban if needed)
//With letter X you can add a server you are in, putting the pass
//You have a menu (pressing I) to set the name of the player you want to ban just pressing letter U. Useful for example to players with VPN

//goadmin (to take admin if you have it)
//akick (kick with admin)
//aban (ban with admin)
//abanss (aban with remember for infoban and screenshot)
//abanname (aban by the name)
//abannamess (abanname with infoban and screenshot)
//set_abanname (to fastly ban trollers)

//Initialize things
checkinit temp_admin_name
checkinit admin_name
checkinit servers_ips_and_ports
checkinit servers_names
checkinit servers_passwords

// SETTING UP SERVERS AND PASSWORDS
bind X [menu_servers_passwords]

checkinit put_original_name
alias menu_servers_passwords [
    //Menu add server
    delmenu "\f0Add\f5 / \f1update\f5 nick / this server \f3admin\f5"
    newmenu "\f0Add\f5 / \f1update\f5 nick / this server \f3admin\f5"
    menuitemtextinput "Global admin nickname " $admin_name [temp_admin_name = $arg1] [] 64
    menuitemcheckbox "Show real nick when admin " $put_original_name [put_original_name = $arg1]
    menuitem "\f2[Save nickname]" [
        closecurmenu

        if (! (strcmp $temp_admin_name "")) [
            admin_name = $temp_admin_name
            echo "\f3Admin name \f0succesfully changed"
        ]
    ]

    if (< (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))) 0) [
        temp_server_password = ""
    ] [
        temp_server_password = (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))))
    ]

    menuitem "" -1

    if (! (strcmp (curserver 6) "")) [
        menuitem (curserver 6)
        menuitemtextinput "Pass " $temp_server_password [temp_server_password = $arg1] [] 64
        menuitem "\f2[Add Server]" [
            closecurmenu

            if (strcmp $temp_server_password "") [
                temp_server_password = "-"
            ]

            if (< (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))) 0) [
                add2list servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)); add2list servers_names (strreplace (curserver 6) " " "_"); add2list servers_passwords $temp_server_password; echo (curserver 6) " \f5("(concatword (curserver 1) ":" (curserver 3))") \f0Added correctly";
            ] [
                servers_names = (strreplace $servers_names (at $servers_names (findlist ($servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))))) (strreplace (curserver 6) " " "_"))
                servers_passwords = (strreplace $servers_passwords (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)))) $temp_server_password)

                echo (concatword (curserver 6) " \f1server updated successfully")
            ]
        ]
    ] [
        menuitem "\f3You are not connected to any server"
    ]

    showmenu "\f0Add\f5 / \f1update\f5 nick / this server \f3admin\f5"
]

checkinit temp_old_name_goadmin

// ADMIN ON THE SERVERS YOU GOT WITH THE SAME KEY
goadmin = [
    //If has got admin
    if (player1 role) [
        if (= $put_original_name 1) [
            name $temp_old_name_goadmin
        ]
        setadmin 0
    ] [
        temp_old_name_goadmin = (player1 name)

        if (>= (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))) 0) [
            if (= $put_original_name 1) [
                name $admin_name
            ]
            
            //If pass is "-" then use claimadmin alone
            if (strcmp (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)))) "-") [
                claimadmin
            ] [
                temp_server_password = (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))))
                setadmin 1 $temp_server_password
            ]
        ] [
            //If you do not have admin
            echo "\f3You \f2don't have admin \f3rights on this server"
        ]
    ]
]

//ADMIN UTILITIES
checkinit temp_reason

bind P [goadmin]

//akick (kick with admin)
akick = [
    if ($votepending) [
        vote 2
    ]
    
    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    
    if (< (strlen $arg2) 4) [
        if (<= (strlen $arg2) 0) [
            temp_reason = "Fast reason (probably troller, hacker or ban evade)"
        ] [
            temp_reason = (concatword $arg2 "...")
        ]
    ]

    goadmin
    sleep 200 [kick $temp_cn_vote $temp_reason]
    sleep 500 [setadmin 0]
    sleep 500 [name $temp_old_name]
]

//aban (ban with admin)
aban = [
    if ($votepending) [
        vote 2
    ]

    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    
    if (< (strlen $arg2) 4) [
        if (<= (strlen $arg2) 0) [
            temp_reason = "Fast reason (probably troller, hacker or ban evade)"
        ] [
            temp_reason = (concatword $arg2 "...")
        ]
    ]

    goadmin
    sleep 200 [ban $temp_cn_vote $temp_reason]
    sleep 500 [setadmin 0]
    sleep 500 [name $temp_old_name]
]

//abanss (aban with infoban and screenshot)
abanss = [
    if ($votepending) [
        vote 2
    ]

    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    
    if (< (strlen $arg2) 4) [
        if (<= (strlen $arg2) 0) [
            temp_reason = "Fast reason (probably troller, hacker or ban evade)"
        ] [
            temp_reason = (concatword $arg2 "...")
        ]
    ]

    infoban $temp_cn_vote
    goadmin
    sleep 200 [ban $temp_cn_vote $temp_reason]
    sleep 250 [toggleconsole]
    sleep 300 [screenshot]
    sleep 500 [setadmin 0]
    sleep 500 [name $temp_old_name]
    sleep 600 [toggleconsole]
]

//abanname (aban by the name)
abanname = [
    temp_cn_vote = (findcn $arg1)
    aban $temp_cn_vote $arg2
]

//abannamess (aban by the name)
abannamess = [
    temp_cn_vote = (findcn $arg1)
    abanss $temp_cn_vote $arg2
]

//set_fast_ban_name (to fastly ban trollers). Always starts empty (or you can fill here), but you can set ingame
fast_ban_name = ""

bind U [abanname $fast_ban_name]
set_fast_ban_name = [
    fast_ban_name = $arg1
]

//Menu for fast ban player name
newmenu "\f3Fast ban player name\f5"
menuitemtextinput "Name" $fast_ban_name [fast_ban_name = $arg1] [] 15
menuitem "\f2[Apply]" [update_fast_ban_player_menu]

update_fast_ban_player_menu = [
    closecurmenu
    delmenu "\f3Fast ban player name\f5"
    newmenu "\f3Fast ban player name\f5"
    menuitemtextinput "Name" $fast_ban_name [fast_ban_name = $arg1] [] 15
    menuitem "\f2[Apply]" [update_fast_ban_player_menu]
]

bind I [showmenu "\f3Fast ban player name\f5"]
